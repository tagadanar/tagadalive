/*
 * Cellule de jeu contenant l'id, 'les coordonnées, les voisins et le type
 */ 
class Cell {
	static integer SELF_CAST_ID = 1312 // sentinel cell ID for self-cast actions (outside valid range 0-612)

	integer id
	boolean isWall
	Array<Cell> neighbors
	Array<Cell> neighborsObstacles
	integer x
	integer y
	Map<integer, Array<Cell>> _AREAS = [:]

	constructor(integer id){
		this.id = id
		if (id == Cell.SELF_CAST_ID) {
			this.isWall = false
			this.x = 18
			this.y = 18
		} else {
			this.isWall = isObstacle(id)
			this.x = getCellX(id)!
			this.y = getCellY(id)!
		}
	}
	
	void init(){
		this.initNeighbors()
		this.initNeighborsObstacles()
	}
	
	/*
	 * Initialise les cellules voisines
	 */
	void initNeighbors(){
		Array<Cell> result = []

		integer? c = getCellFromXY(x, y-1);
		if(c!=null && !Board.obstacles[Board.cells[c!]!]) push(result, Board.getCell(c));
		c = getCellFromXY(x, y+1);
		if(c!=null && !Board.obstacles[Board.cells[c!]!]) push(result, Board.getCell(c));
		c = getCellFromXY(x-1, y);
		if(c!=null && !Board.obstacles[Board.cells[c!]!]) push(result, Board.getCell(c));
		c = getCellFromXY(x+1, y);
		if(c!=null && !Board.obstacles[Board.cells[c!]!]) push(result, Board.getCell(c));

		this.neighbors = result
	}
	
	void initNeighborsObstacles(){
		Array<Cell> result = []

		integer? c = getCellFromXY(x, y-1);
		if(c!=null) push(result, Board.getCell(c));
		c = getCellFromXY(x, y+1);
		if(c!=null) push(result, Board.getCell(c));
		c = getCellFromXY(x-1, y);
		if(c!=null) push(result, Board.getCell(c));
		c = getCellFromXY(x+1, y);
		if(c!=null) push(result, Board.getCell(c));

		this.neighborsObstacles = result
	}

	/*
	 * Retourne un tableau de Cell correspondant aux cases de l'@area en params depuis cette case
	 * Lazy initialization: compute on first access
	 */
	Array<Cell> getAreaCells(integer area){
		if(_AREAS[area] == null) {
			_AREAS[area] = this._computeArea(area)
		}
		return _AREAS[area]!
	}
	
	/*
	 * Compute a single area on-demand (lazy initialization)
	 */
	private Array<Cell> _computeArea(integer area) {
		integer? c
		Array<Cell> result = []

		if (area == AREA_POINT || area == AREA_FIRST_INLINE) {
			return [this]
		}

		if (area == AREA_CIRCLE_1) {
			push(result, this)
			c = getCellFromXY(x, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_PLUS_2) {
			result = clone(this.getAreaCells(AREA_CIRCLE_1)) as Array<Cell>
			c = getCellFromXY(x, y-2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x, y+2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-2, y); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+2, y); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_CIRCLE_2) {
			result = clone(this.getAreaCells(AREA_PLUS_2)) as Array<Cell>
			c = getCellFromXY(x+1, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y+1); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_CIRCLE_3) {
			result = clone(this.getAreaCells(AREA_CIRCLE_2)) as Array<Cell>
			c = getCellFromXY(x+2, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+2, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y-2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y+2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y-2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y+2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-2, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-2, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x, y-3); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x, y+3); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-3, y); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+3, y); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_PLUS_3) {
			result = clone(this.getAreaCells(AREA_PLUS_2)) as Array<Cell>
			c = getCellFromXY(x, y-3); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x, y+3); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-3, y); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+3, y); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_SQUARE_1) {
			result = clone(this.getAreaCells(AREA_CIRCLE_1)) as Array<Cell>
			c = getCellFromXY(x-1, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y-1); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_SQUARE_2) {
			result = clone(this.getAreaCells(AREA_CIRCLE_2)) as Array<Cell>
			c = getCellFromXY(x+2, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+2, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y-2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y+2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y-2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y+2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-2, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-2, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-2, y+2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-2, y-2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+2, y-2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+2, y+2); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_X_1) {
			c = getCellFromXY(x-1, y-1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-1, y+1); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+1, y-1); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_X_2) {
			result = clone(this.getAreaCells(AREA_X_1)) as Array<Cell>
			c = getCellFromXY(x-2, y-2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+2, y+2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-2, y+2); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+2, y-2); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		if (area == AREA_X_3) {
			result = clone(this.getAreaCells(AREA_X_2)) as Array<Cell>
			c = getCellFromXY(x-3, y-3); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+3, y+3); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x-3, y+3); if(c!=null) push(result, Board.getCell(c)!);
			c = getCellFromXY(x+3, y-3); if(c!=null) push(result, Board.getCell(c)!);
			return result
		}

		debugE('unhandled AREA in Cell._computeArea(): ' + area)
		return [this]
	}

	/*
	 * Format chaîne de caractères utilisée pour des tests / debugs.
	 */
	string(){
		return "<Cell "+this.id+">"
	}
}