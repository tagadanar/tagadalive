/*
 * Objet contenant les informations correspondante a une item
 * On en instancie un pour chacun des équipements du jeu
 */
class Item {
	integer id
	string name
	boolean isWeap
	boolean haveCD
	integer cdDuration = 0
	integer minRange
	integer maxRange
	integer launchType
	boolean needLOS
	integer cost
	integer area
	boolean isAOE
	boolean onCaster = false
	integer maxUse  // Max uses per round (from API)
	Array<ItemEffect> effects = []
	integer priority  // Execution order priority (lower = first)

	integer targetKey
	boolean targetEmptyCell = false
	boolean isSummon = false
	boolean isTactical = false
	boolean isOffensive = false  // Can deal damage/debuff to enemies (O(1) check)
	boolean isDamage = false     // Has EFFECT_DAMAGE or EFFECT_LIFE_DAMAGE
	boolean worksOnInvincible = false  // Shackle items work on invincible targets

	static integer ALL = 0
	static integer ALLLEEKS = 1
	static integer ALLBULBS = 2
	static integer ALLIES = 3
	static integer ALLIESLEEKS = 4
	static integer ALLIESBULBS = 5
	static integer ENEMIES = 6
	static integer ENEMIESLEEKS = 7
	static integer ENEMIESBULBS = 8
	static integer NONE = 9

	// Default priority for items not in PRIORITIES map
	static integer UNKNOWN_PRIORITY = 50

	// Priority groups (suggested consolidation for future reference):
	// -1: Antidote (before Liberation - full poison removal)
	// 0: Debuff removal (Liberation)
	// 1: Inversion (position swap before summons)
	// 2: Summons, resurrection (early, so they can receive buffs)
	// 3: Agility buffs (crits for subsequent attacks)
	// 4-5: TP/MP generation
	// 6-7: MP buffs
	// 8-10: Strength/Magic buffs (before damage)
	// 11-12: Resistance/Wisdom (before heals/armor)
	// 13: Armor reduction (before main damage)
	// 14: Armor application
	// 15-18: Damage, poison, debuffs
	// 19-20: Heals, damage reflection
	static Map<integer, integer> PRIORITIES = [
		CHIP_ANTIDOTE: -1,           // Before Liberation - full poison removal
		CHIP_MANUMISSION: -1,		 // hmmm avant ou après libe ? xD
		CHIP_LIBERATION: 0,          // Removes debuffs (after Antidote)
		CHIP_INVERSION: 1,           // Position swap (before summons)
		CHIP_LIGHTNING_BULB: 2,      // Summons early (so bulb can receive buffs)
		CHIP_WIZARD_BULB: 2,
		CHIP_FIRE_BULB: 2,
		CHIP_METALLIC_BULB: 2,
		CHIP_HEALER_BULB: 2,
		CHIP_ICED_BULB: 2,
		CHIP_ROCKY_BULB: 2,
		CHIP_PUNY_BULB: 2,
		CHIP_TACTICIAN_BULB: 2,
		CHIP_SAVANT_BULB: 2,
		CHIP_RESURRECTION: 2,        // Resurrection early (like summons)
		CHIP_REFLEXES: 3,            // Agility buffs (crits)
		CHIP_STRETCHING: 3,
		CHIP_WARM_UP: 3,
		CHIP_RAGE: 4,                // TP generation
		CHIP_MOTIVATION: 4,
		CHIP_COVETOUSNESS: 4,
		CHIP_ADRENALINE: 4,
		CHIP_WHIP: 5,
		CHIP_SEVEN_LEAGUE_BOOTS: 6,  // MP buffs
		CHIP_PRECIPITATION: 6,
		CHIP_ACCELERATION: 6,
		CHIP_LEATHER_BOOTS: 6,
		CHIP_WINGED_BOOTS: 7,
		CHIP_WIZARDRY: 8,
		CHIP_DOPING: 9,              // Strength (before damage)
		CHIP_PROTEIN: 9,
		CHIP_PRISM: 9,
		CHIP_FEROCITY: 10,
		CHIP_STEROID: 10,
		CHIP_SOLIDIFICATION: 11,     // Resistance (before armor)
		CHIP_BARK: 11,
		CHIP_COLLAR: 12,             // Wisdom (before heals)
		CHIP_KNOWLEDGE: 12,
		WEAPON_J_LASER: 13,          // Armor reduction (before main damage)
		WEAPON_SHOTGUN: 13,
		WEAPON_MYSTERIOUS_ELECTRISOR: 13,
		CHIP_CARAPACE: 14,           // Armor application
		CHIP_FORTRESS: 14,
		CHIP_ARMOR: 14,
		CHIP_RAMPART: 14,
		CHIP_SHIELD: 14,
		CHIP_WALL: 14,
		CHIP_HELMET: 14,
		CHIP_DOME: 14,
		CHIP_BURNING: 15,            // Burning (cleans metal before damage)
		CHIP_DEVIL_STRIKE: 16,       // Self-damage attacks
		WEAPON_NEUTRINO:16,			 // vuln before dmg
		CHIP_METEORITE: 17,          // Main damage
		CHIP_ICEBERG: 17,
		CHIP_ROCKFALL: 17,
		CHIP_LIGHTNING: 17,
		CHIP_STALACTITE: 17,
		CHIP_FLAME: 17,
		CHIP_ROCK: 17,
		CHIP_ICE: 17,
		CHIP_SPARK: 17,
		CHIP_PEBBLE: 17,
		CHIP_SHOCK: 17,
		WEAPON_AXE: 17,
		WEAPON_B_LASER: 17,
		WEAPON_BROADSWORD: 17,
		WEAPON_DESTROYER: 17,
		WEAPON_DOUBLE_GUN: 17,
		WEAPON_ELECTRISOR: 17,
		WEAPON_RHINO: 17,
		WEAPON_RIFLE: 17,
		WEAPON_GRENADE_LAUNCHER: 17,
		WEAPON_KATANA: 17,
		WEAPON_LASER: 17,
		WEAPON_MACHINE_GUN: 17,
		WEAPON_MAGNUM: 17,
		WEAPON_M_LASER: 17,
		WEAPON_PISTOL: 17,
		WEAPON_UNBRIDLED_GAZOR: 17,
		WEAPON_ILLICIT_GRENADE_LAUNCHER: 17,
		WEAPON_LIGHTNINGER: 17,
		WEAPON_BAZOOKA: 17,
		WEAPON_DARK_KATANA: 17,
		WEAPON_ENHANCED_LIGHTNINGER: 17,
		WEAPON_UNSTABLE_DESTROYER: 17,
		WEAPON_SWORD: 17,
		WEAPON_HEAVY_SWORD: 17,
		WEAPON_ODACHI: 17,
		WEAPON_EXCALIBUR: 17,
		WEAPON_SCYTHE: 17,
		CHIP_FLASH: 17,
		CHIP_PUNISHMENT: 17,
		CHIP_PLASMA: 17,
		CHIP_ARSENIC: 18,            // Poison by priority (strongest first)
		CHIP_COVID: 19,
		CHIP_PLAGUE: 20,
		CHIP_TOXIN: 21,
		WEAPON_GAZOR: 22,
		WEAPON_FLAME_THROWER: 23,
		CHIP_VENOM: 24,
		WEAPON_REVOKED_M_LASER: 24,
		CHIP_BALL_AND_CHAIN: 25,     // Debuffs (after poison)
		CHIP_SLOW_DOWN: 26,
		CHIP_SOPORIFIC: 27,          // Remaining debuffs
		CHIP_TRANQUILIZER: 27,
		CHIP_FRACTURE: 27,
		CHIP_CRUSHING: 27,
		CHIP_BRAINWASHING: 27,
		CHIP_TRANSMUTATION: 16,      // Buffs before damage
		CHIP_MUTATION: 16,
		WEAPON_QUANTUM_RIFLE: 28,	 // Nova dmg after dmg
		CHIP_DESINTEGRATION: 28,
		WEAPON_EXPLORER_RIFLE: 28,
		CHIP_REGENERATION: 29,       // Heals by priority
		CHIP_THERAPY: 30,
		CHIP_REMISSION: 31,
		CHIP_VAMPIRIZATION: 32,
		CHIP_DRIP: 33,
		CHIP_CURE: 34,
		CHIP_BANDAGE: 35,
		CHIP_SERUM: 36,              // Buffs (same)
		CHIP_VACCINE: 36,
		CHIP_ELEVATION: 37,          // Stat buffs (same)
		CHIP_ARMORING: 37,
		CHIP_FERTILIZER: 37,
		CHIP_LOAM: 37,
		CHIP_ALTERATION: 37,
		CHIP_MIRROR: 38,             // Damage reflection
		CHIP_THORN: 38,
		CHIP_BRAMBLE: 38,
		CHIP_JUMP: 39,               // Movement/utility
		CHIP_REPOTTING: 39,
		CHIP_GRAPPLE: 39,
		CHIP_BOXING_GLOVE: 39,
		CHIP_TELEPORTATION: 39,
		CHIP_APOCALYPSE: 40,         // Boss chips
		CHIP_KILL: 40,
		CHIP_DIVINE_PROTECTION: 40,
		415: 40, // CHIP_AWAKENING
		425: 40, // CHIP_EXASPERATION
		411: 40, // unknown shuriken
		412: 40, // unknown kemuridama
		413: 40, // unknown fire_ball
		414: 40, // unknown trebuchet
		416: 40, // unknown thunder
	]
			
	constructor(integer id) {
		this.id = id
		this.name = getWeaponName(id)+getChipName(id)

		// Lookup priority from static map, log unknown items
		if (Item.PRIORITIES[id] != null) {
			this.priority = Item.PRIORITIES[id]!
		} else {
			this.priority = Item.UNKNOWN_PRIORITY
			debugW("Unknown item priority: " + id + " (" + this.name + ")")
		}

		this.isWeap = isWeapon(id)
		if(this.isWeap){
			this.haveCD = false
			this.minRange = getWeaponMinRange(id)
			this.maxRange = getWeaponMaxRange(id)
			this.launchType = getWeaponLaunchType(id)!
			this.needLOS = weaponNeedLos(id)
			this.cost = getWeaponCost(id)
			this.area = getWeaponArea(id)!
			this.maxUse = getWeaponMaxUses(id)
			integer i = 0
			for(Array<real | integer> e in getWeaponEffects(id)!){
				push(this.effects, ItemEffect(e, this, i++))
			}
		} else {
			this.cdDuration = getChipCooldown(id)
			// Handle infinite cooldown (-1) - can only use once per fight
			if (this.cdDuration == -1) this.cdDuration = ScoringConfig.MAX_FIGHT_TURNS
			this.haveCD = this.cdDuration != 0
			this.minRange = getChipMinRange(id)!
			this.maxRange = getChipMaxRange(id)!
			this.launchType = getChipLaunchType(id)!
			this.needLOS = chipNeedLos(id)
			this.cost = getChipCost(id)!
			this.area = getChipArea(id)!
			this.maxUse = getChipMaxUses(id)!
			if (this.maxUse == -1) this.maxUse = this.haveCD ? 1 : 99
			// Tranquilizer: force 1 use — debuff doesn't stack, ComboExplorer wastes TP repeating it
			if (id == CHIP_TRANQUILIZER) this.maxUse = 1
			integer i = 0
			for(Array<real | integer> e in getChipEffects(id)!){
				push(this.effects, ItemEffect(e, this, i++))
			}
		}

		// Detect summon, tactical, and damage chips
		for (ItemEffect e in this.effects) {
			if (e.type == EFFECT_SUMMON || e.type == EFFECT_RESURRECT) {
				this.isSummon = true
			}
			if (e.type == EFFECT_INVERT ||
				e.type == EFFECT_TELEPORT ||
				e.type == EFFECT_PUSH ||
				e.type == EFFECT_ATTRACT) {
				this.isTactical = true
			}
			if (e.type == EFFECT_DAMAGE || e.type == EFFECT_LIFE_DAMAGE) {
				this.isDamage = true
			}
			if (e.type == EFFECT_SHACKLE_STRENGTH ||
				e.type == EFFECT_SHACKLE_MAGIC ||
				e.type == EFFECT_SHACKLE_AGILITY ||
				e.type == EFFECT_SHACKLE_WISDOM ||
				e.type == EFFECT_SHACKLE_TP ||
				e.type == EFFECT_SHACKLE_MP) {
				this.worksOnInvincible = true
			}
		}

		if(this.area == AREA_POINT || this.area == AREA_LASER_LINE) this.isAOE = false
		else this.isAOE = true
		// on cherche à trouver quels sont les cibles valides et intéressante de l'item
		// on set targetKey pour que targetSet pointe vers la fonction qui renvera uniquement les entités intéressante à cibler
		boolean allies = false, enemies = false, onBulbs = false, onLeeks = false;
		for(ItemEffect e in this.effects){
			if(!allies) allies = e.targetType.allies
			if(!enemies) enemies = e.targetType.enemies
			if(!onBulbs) onBulbs = e.targetType.onBulbs
			if(!onLeeks) onLeeks = e.targetType.onLeeks
			if(!this.onCaster) this.onCaster = e.targetType.onCaster
		}
		
		if(false){ // TODO FAIRE UNE PASSE SUR LES ITEMS POUR CHECK SI TOUT EST COMME JE LE SOUHAITE §
			// + coder les items chelous dans TargetType
			debug(this)
			debug("allies:"+allies)
			debug("enemies:"+enemies)
			debug("onBulbs:"+onBulbs)
			debug("onLeeks:"+onLeeks)
			pause()
		}
		
		// si c'est un lazer, je veux potentiellement tirer à travers tout le monde ^^
		if(this.area == AREA_LASER_LINE){
			allies = true
			enemies = true
			onBulbs = true
			onLeeks = true
		}
		
		if(allies && enemies){ // ALL
			if(onLeeks && onBulbs) this.targetKey = Item.ALL
			else if(onLeeks && !onBulbs) this.targetKey = Item.ALLLEEKS
			else if(!onLeeks && onBulbs) this.targetKey = Item.ALLBULBS
			else{
				debugW('strange targetKey in <Item> for '+this)
				this.targetKey = Item.NONE
			}
		} else if(allies && !enemies){ // only ALLIES
			if(onLeeks && onBulbs) this.targetKey = Item.ALLIES
			else if(onLeeks && !onBulbs) this.targetKey = Item.ALLIESLEEKS
			else if(!onLeeks && onBulbs) this.targetKey = Item.ALLIESBULBS
			else{
				debugW('strange targetKey in <Item> for '+this)
				this.targetKey = Item.NONE
			}

		} else if(!allies && enemies){ // only ENEMIES
			if(onLeeks && onBulbs) this.targetKey = Item.ENEMIES
			else if(onLeeks && !onBulbs) this.targetKey = Item.ENEMIESLEEKS
			else if(!onLeeks && onBulbs) this.targetKey = Item.ENEMIESBULBS
			else{
				debugW('strange targetKey in <Item> for '+this)
				this.targetKey = Item.NONE
			}
		} else { // none: cas des actions avec des items de déplacements teleport, ou des invocs
			this.targetKey = Item.NONE
			targetEmptyCell = true // TODO handle this param in IA
		}

		// Set isOffensive: can deal damage/debuff to enemies (used for O(1) filtering)
		this.isOffensive = enemies && !this.isSummon && !this.isTactical
	}
	
	Map<integer, Entity> targetSet(){
		if(this.targetKey==Item.ALL) return Fight.getAllAlive()
		if(this.targetKey==Item.ALLLEEKS) return Fight.getAllLeeksAlive()
		if(this.targetKey==Item.ALLBULBS) return Fight.getAllBulbsAlive()
		if(this.targetKey==Item.ALLIES) return Fight.getAlliesAlive()
		if(this.targetKey==Item.ALLIESLEEKS) return Fight.getAlliesLeeksAlive()
		if(this.targetKey==Item.ALLIESBULBS) return Fight.getAlliesBulbsAlive()
		if(this.targetKey==Item.ENEMIES) return Fight.getEnemiesAlive()
		if(this.targetKey==Item.ENEMIESLEEKS) return Fight.getEnemiesLeeksAlive()
		if(this.targetKey==Item.ENEMIESBULBS) return Fight.getEnemiesBulbsAlive()
		if(this.targetKey==Item.NONE) return [:]
	}
	
	/*
	 * tente d'utiliser l'item
	 * @param cell cellule de type Cell représentant l'endroit ou je veux utiliser l'Item avec id: (this.id)
	 */
	void useItemOnCell(Cell cell){
		if(this.isWeap){
			if(getWeapon(Fight.self.id)!=this.id) setWeapon(this.id)
			useWeaponOnCell(cell.id)
		} else useChipOnCell(this.id, cell.id)
	}

	/*
	 * Same as useItemOnCell but returns the result code
	 */
	integer useItemOnCellWithResult(Cell cell){
		if(this.isWeap){
			if(getWeapon(Fight.self.id)!=this.id) setWeapon(this.id)
			return useWeaponOnCell(cell.id)
		} else return useChipOnCell(this.id, cell.id)
	}
	
	/*
	 * tente d'utiliser l'item
	 * @param cell cellule de type Cell représentant l'endroit ou je veux utiliser l'Item avec id: (this.id)
	 */
	void useItem(Entity entity){
		if(this.isWeap){
			if(getWeapon(Fight.self.id)!=this.id) setWeapon(this.id)
			useWeapon(entity.id)
		} else useChip(this.id, entity.id)
	}
	
	/*
	 * Format chaîne de caracteres utilisée pour des tests / debugs.
	 */
	string() {
		return "<Item "+this.name+">"
	}
}